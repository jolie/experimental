/***************************************************************************
 *   Author: Diego Castronuovo                                             *
 *   An Xtext grammar for the programming language Jolie                   *
 ***************************************************************************/
grammar jolie.xtext.Jolie hidden(WS, ML_COMMENT, SL_COMMENT)

generate jolie "http://www.xtext.org/Jolie"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Program:
	(constants+=Constant |
	include+=Include |
	ports+=Port |
	interface+=Interface |
	types+=Type |
	init+=Init |
	execution+=Execution |
	define+=Define |
	embedded+=Embedded)*
	main=Main?;




/****************************************Types****************************************/
Constant:
	'constants' '{' name+=ID ASSIGN (INT | STRING | REAL | ID) (COMMA name+=ID ASSIGN (INT | STRING | REAL | ID))* '}';

Execution:
	'execution' '{' (CONCURRENT | SEQUENTIAL) '}';

Include:
	'include' importURI=STRING;

FileName:
	name=STRING;

Type:
	'type' name=ID COLON native_type_sub+=Native_type typedef+=Typedef?;

Typedef:
	{Typedef} '{'
	subtypes+=Subtypes
	'}';

Subtypes:
	{Subtypes} (DOT name+=ID Cardinality? COLON native_type+=Native_type typedef+=Typedef*)* | {Subtypes}
	QUESTION;

Cardinality:
	QUESTION | ASTERISK | ('[' INT COMMA (INT | ASTERISK) ']');

 Native_type:
	 name+=("any" | "int" | "real" | "string" | "void" | 'undefined' | 'double' | 'raw' )| type=[Type];

/**********************************Types***********************************************/
Embedded:
	'embedded' '{' ('Java' | 'Jolie' | 'Javascript') COLON string+=STRING (in+=ID name+=ID)? (COMMA string+=STRING (in+=ID
	name+=ID)?)* '}';

Define:
	'define' name=ID mainrocess=MainProcess;

Init:
	name+='init' mainrocess=MainProcess;

Main:
	name='main' mainrocess=MainProcess;
	//Il processo principale, {...} del main
MainProcess:
	{ParallelStatement} '{' parallelStatement=ParallelStatement '}';

	//Il processo generale inseribile a piacimento nel codice {...},(...)
Process:
	{Process} '{' parallelStatement+=ParallelStatement '}' | '(' parallelStatement+=ParallelStatement ')';

	//Un parallel contiene una o pi sequenze separate da VERT '|'
ParallelStatement:
	{ParallelStatement} (sequenceStatement+=SequenceStatement (VERT sequenceStatement+=SequenceStatement)*);

	//Un sequence contiente uno o pi basic statement separati da SEMICOLON ';'
SequenceStatement:
	{SequenceStatement} (basicStatement+=BasicStatement (SEMICOLON basicStatement+=BasicStatement)*);

NDChoiceStatement:
/*
      
      [linkIn(linkGuard)|inputOp1] {...}
      [inputOp2] {...}      
      
     */
	{NDChoiceStatement} ('[' (('linkIn' '(' ID ')') | (operation+=[OneWayOperationSignature] |
	operation+=[RequestResponseSignature] op+=InputOrOutputOperationDefOrCall)) ']' mainProcess+=MainProcess)+;

	//Il basicStatement,  un costrutto base, ma pu anche essere un processo (questa produzione permette la ricorsione di parallel e sequence) 
BasicStatement:
	{Process} process=Process | //Questa  la regola che mi permette la ricorsione
	{BasicStatement} assignStatementOrPostIncrementDecrement=AssignStatementOrPostIncrementDecrementOrInputOperation |
	NDChoiceStatement=NDChoiceStatement | preIncrementDecrement=PreIncrementDecrement | With | Synchronized | Undef | For
	| If | Foreach | While | {BasicStatement} 'nullProcess' | linkIn | linkOut | call=[Define] |
	operation=[OneWayOperationSignature] | operation=[RequestResponseSignature] op=InputOrOutputOperationDefOrCall | // TODO Refactor... 
	scope=Scope | compensate=Compensate | throw=Throw | install=Install | {cH} cH | {Exit} Exit;

Is_function:
	{Is_function} ('is_defined' | 'is_string' | 'is_double' | 'is_int') '(' variablePath=VariablePath ')';

Install:
	'install' '(' installFunction=InstallFunciton ')';

Throw:
	'throw' '(' name+=ID (COMMA espression+=Expression)* ')';

Compensate:
	'comp' name=ID mainProcess=MainProcess;

Scope:
	'scope' '(' name=ID ')' mainProcess=MainProcess;

InputOrOutputOperationDefOrCall:
	'(' variablePath=VariablePath? ')' inputOperation=InputOperation /*Use the validator here to verify oneWay/reqOP*/ |
	AT outputPortCall=OutputPortCall;

linkIn:
	{linkIn} 'linkIn' '(' name=ID ')';

linkOut:
	{linkOut} 'linkOut' '(' name=ID ')';

cH:
	'cH';

Exit:
	'exit';

AssignStatementOrPostIncrementDecrementOrInputOperation:
	 variablePath=VariablePath rightSide=RightSide;

RightSide:
	{RightSide} ASSIGN expression=Expression | {RightSide} CHOICE | {RightSide} DECREMENT | {RightSide} POINTSTO
	variablePath=VariablePath | {RightSide} DEEPCOPYLEFT variablePath=VariablePath;

/******************************************* BASIC STATEMENTS*********************************************/
Synchronized:
	'synchronized' '(' name+=ID ')' mainProcess+=MainProcess;

Undef:
	'undef' '(' variablePath=VariablePath ')';

OutputPortCall:
// TODO Validare che la porta contenga l'operazione precedente
// TODO Validare che se l'operation  one way solo () mentre req resp ()()
	port=[InputPortStatement] | port=[OutputPortStatement] '(' expression=Expression? ')' /*Notification Operation*/ ('('
	(variablePath=VariablePath)? ')'
	/*Solicit Response operation*/
	('[' installFunction=InstallFunciton ']')?)?;

InstallFunciton:
	(name+=ID | 'this') '=>' parallelStatement+=ParallelStatement (COMMA (name+=ID | 'this') '=>'
	parallelStatement+=ParallelStatement)*;

/*Input operation: OneWay o ReqResp*/
InputOperation:
	{InputOperation} ('(' expression=Expression? ')' mainProcess=MainProcess)?; //line 1689 OLParser, with ? ReqResponseOperation if no ? OneWayOperation
PreIncrementDecrement:
	(CHOICE | DECREMENT) variablePath=VariablePath;

/****************************************************** LOOPS *************************************************/
If:
	'if' '(' condition+=Condition ')' ifProcess=BasicStatement ('else' elseProcess=BasicStatement | ifNasted=If)?;

For:
	'for' '(' parallelStatement+=ParallelStatement COMMA condition=Condition COMMA parallelStatement+=ParallelStatement
	body=Body;

Body:
	')' BasicStatement;

Condition:
	NOT condition+=Condition | '(' condition+=Condition ')' (('&&' | '||') condition+=Condition)? |
	{Condition} (variablePath=VariablePath | INT | STRING | isF=Is_function) rightCondition=RightCondition? (('&&' | '||')
	condition+=Condition)?;

RightCondition:
	(EQUAL | LANGLE | RANGLE | MAJOR_OR_EQUAL | MINOR_OR_EQUAL | NOT_EQUAL) expression=Expression;

Foreach:
	{Foreach} 'foreach' '(' var1=VariablePath COLON var2=VariablePath body=Body;

While:
	'while' '(' condition+=Condition ')' mainProcess=MainProcess;

/***************************************************************************************************************/

/*********************************************ESPRESSIONI ARITMETICHE*********************************************/
Expression:
	TerminalExpression ({Operation.left=current} op=(PLUS | MINUS | ASTERISK | DIVIDE) right=Expression)?;

TerminalExpression returns Expression:
	'(' Expression ')' | {IntLiteral} MINUS? value=INT | {RealLiteral} MINUS? value=REAL | {String} value=STRING |
	(CHOICE |
	DECREMENT) variablePath=VariablePath | variablePath=VariablePath (CHOICE | DECREMENT)?;

VariablePath:
	{VariablePath} HASH? dot+=DOT? name+=(ID) ('[' children+=Expression ']')? (((DOT (ID | 'global') ('['
	children+=Expression
	']')?) | (DOT '(' children+=Expression ')')))* | {VariablePath} 'global' (((DOT (ID | 'global') ('['
	children+=Expression ']')?) | (DOT '(' children+=Expression ')')))* //Global non  un un array
;

With:
	{With} 'with' '(' name=VariablePath ')' mainrocess=MainProcess;

/******************************************************************************************************************/

/*********************************************NDChoiceStatement*********************************************/
/******************************************************************************************************************/
Interface:
	'interface' name=ID '{'
	(RequestResponseOperation+=RequestResponseOperation? & oneWayOperation+=OneWayOperation?)
	'}';

/********************************************InputPortStatement & OutputPortStatement*************************************************/
Port:
	(inputPortStatement+=InputPortStatement | outputPortStatement+=OutputPortStatement);

InputPortStatement:
	'inputPort' name=ID '{' ((location+=Location)? & (protocol+=Protocol)? & (oneWayOperation+=OneWayOperation)? &
	(RequestResponseOperation+=RequestResponseOperation)? & (redirects+=Redirects)? & (aggregates+=Aggregates)? &
	(intefaces=Interfaces)?) '}';

OutputPortStatement:
	'outputPort' name=ID '{' ((location+=Location)? & (protocol+=Protocol)? & (oneWayOperation+=OneWayOperation)? &
	(RequestResponseOperation+=RequestResponseOperation)? & (intefaces+=Interfaces)?) '}';

OneWayOperation:
	{OneWayOperation} 'OneWay' COLON name=OneWayOperationSignature;

RequestResponseOperation:
	{RequestResponseOperation} 'RequestResponse' COLON name=RequestResponseSignature;

OneWayOperationSignature:
	{OneWayOperationSignature} name=ID ('(' TypeOfThrow+=TypeOfThrow ')')? (COMMA op=OneWayOperationSignature)?;

RequestResponseSignature:
	{RequestResponseSignature} name=ID ('(' TypeOfThrow+=TypeOfThrow ')' '(' TypeOfThrow+=TypeOfThrow ')')? ('throws'
	(faults+=ThrowsClause)+)?
	(COMMA op=RequestResponseSignature)?;

ThrowsClause:
	name+=ID ('(' (type=TypeOfThrow) ')')?;

TypeOfThrow:
	type=[Type] | {TypeOfThrow} naiveType=Native_type;

Location:
	'Location' COLON uri+=Uri;

Uri:
	{Uri} name+=ID | {Uri} STRING; //|name=STRING; //to do...
Interfaces:
	'Interfaces' COLON interface+=[Interface] (COMMA interface+=[Interface])*;

Protocol:
	'Protocol' COLON name+=ID protocolConfiguration=ProtocolConfiguration?;

ProtocolConfiguration:
//Here the validator shuld permit the use of prefixed 
	mainProcess=MainProcess;

Redirects:
	'Redirects' COLON redRef+=RedirectDef (COMMA redRef+=RedirectDef)*; //TODO LINKING TO OutPutPortDef

RedirectDef:
	name=ID '=>' outputPortIdentifier=ID;

Aggregates:
	'Aggregates' COLON name+=ID (COMMA name+=ID)*;

/***********************************************END InpuPortStatement**********************************************************************/
terminal CONCURRENT:
	'concurrent';

terminal SEQUENTIAL:
	'sequential';

terminal SEMICOLON:
	';';

terminal COLON:
	':';

terminal PLUS:
	'+';

terminal VERT:
	'|';

terminal ASSIGN:
	'=';

terminal DOT:
	'.';

terminal COMMA:
	',';

terminal AT:
	'@';

terminal CHOICE:
	'++';

terminal DECREMENT:
	'--';

terminal ASTERISK:
	'*';

terminal QUESTION:
	'?';

terminal DIVIDE:
	'/';

terminal POINTSTO:
	'->';

terminal DEEPCOPYLEFT:
	'<<';

terminal MINUS:
	'-';

terminal PERCENT_SIGN:
	'%';

terminal EQUAL:
	'==';

terminal LANGLE:
	'<';

terminal RANGLE:
	'>';

terminal HASH:
	'#';

terminal MAJOR_OR_EQUAL:
	'>=';

terminal MINOR_OR_EQUAL:
	'<=';

terminal NOT_EQUAL:
	'!=';

terminal NOT:
	'!';

terminal REAL:
	('0'..'9')* DOT ('0'..'9')+ (('e' | 'E') ('0'..'9')+)?;

terminal ID:
	('^'? ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*);

terminal INT returns ecore::EInt:
	('0'..'9')+;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | '"' | "'" | '\\') | !('\\' | "'"))* "'";

terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT:
	'//' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;

terminal ANY_OTHER:
	.;